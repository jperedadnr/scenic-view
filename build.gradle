plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

application {
    mainClass = "org.scenicview.ScenicView"
    mainModule = "org.scenicview.scenicview"
}
group = 'org.scenic-view'
version = '22-ea'

defaultTasks 'install'

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

configurations {
    jdk
    binTray
}

sourceSets {
    main {
        compileClasspath += configurations.jdk
    }
}

javafx {
    version = "22-ea+16"
    modules = ['javafx.web', 'javafx.fxml', 'javafx.swing']
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.scenicview.ScenicView',            /* Used for standalone Scenic View */
                'Agent-Class': 'org.fxconnector.remote.RuntimeAttach',/* But the standalone Scenic View starts up this agent when in runtime attach mode */
                'Premain-Class': 'org.scenicview.ScenicView',          /* Alternatively, this will be called if Scenic View is started as an agent */
                'Automatic-Module-Name': 'org.scenicview.scenicview'
        )
    }
//    archiveName = 'scenicview.jar'
}

run {
	dependsOn jar
}

artifacts {
    archives(jar)
}

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'scenicView'
    }
    imageDir = file("${buildDir}/scenicview")
    imageZip = file("${buildDir}/dist/scenicview-${platform}.zip")
}

tasks.jlink.doLast {
    // Copy scenicView.jar to lib, it is required as loading Agent
    copy {
        from "${buildDir}/libs/scenicview.jar"
        into "${buildDir}/scenicview/lib"
    }
}